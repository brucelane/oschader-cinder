#version 430
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable

#define WORK_GROUP_SIZE 128

uniform float i_delta;
uniform float i_separation;
uniform float i_cohesion;
uniform float i_alignment;
uniform float i_speed;

uniform float numParticles;

layout( std140, binding = 1 ) buffer Pos {
	vec4 pos[];
};

layout( std140, binding = 2 ) buffer Vel {
	vec4 vel[];
};

layout( local_size_x = WORK_GROUP_SIZE,  local_size_y = 1, local_size_z = 1 ) in;

const float PI = 3.141592654;
const float PI_2 = PI * 2.0;

const float WIDTH = 64.0;

const float UPPER_BOUNDS = 400.0;
const float LOWER_BOUNDS = -UPPER_BOUNDS;

vec3 hsv2rgb(vec3 c) {
  vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

vec3 rgb2hsv(vec3 c) {
  vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
  vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
  vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

  float d = q.x - min(q.w, q.y);
  float e = 1.0e-10;
  return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 calculatePosition(vec3 position, vec3 velocity) {
  float delt = min(i_delta, 0.032);
  vec3 newPos = position.xyz + velocity * delt;
  return newPos;
}

vec4 calculateVelocity(vec3 position, vec3 velocity, int current) {
  float delt = min(0.032, i_delta);
  float cd = i_cohesion;
  float sd = i_separation;
  float ad = i_alignment;

  float zoneRadius = sd + ad + cd;
  float separationThresh = sd / zoneRadius;
  float alignmentThresh = (sd + ad) / zoneRadius;
  float zoneRadiusSquared = zoneRadius * zoneRadius;

  vec3 pointPosition, pointVelocity;

  float dist;
  vec3 dir;
  float distSquared;

  float separationSquared = sd * sd;
  float cohesionSquared = cd * cd;

  float percent;

  //TODO: Predator

  vec3 central = vec3(0.0, 0.0, 0.0);
  dir = position.xyz - central;
  distSquared = dot(dir, dir);

  velocity -= normalize(dir) * delt * 0.2 * distSquared;

  // NOTE: Can't make this more efficient because there's no guaranteed run order
  for(int i=0; i < numParticles; i++) {
      pointPosition = pos[i].xyz; 
      pointVelocity = vel[i].xyz;
      dir = pointPosition.xyz - position.xyz;
	  distSquared = dot(dir, dir);

	  float f = 0.8;

      if(distSquared > 0.001 && distSquared < zoneRadiusSquared) {
        percent = distSquared / zoneRadiusSquared;

        if(percent < separationThresh) {
          // Separate
          f *= (separationThresh / percent - 1.0) * delt * 0.7;
          velocity -= normalize(dir) * f;
        }
        else if (percent < alignmentThresh){
          // Align
          float threshi_delta = alignmentThresh - separationThresh;
          float adjustedPercent = (percent - separationThresh) / threshi_delta;

          f *= (0.5 - cos(adjustedPercent * PI_2) * 0.5 + 0.5) * delt * 0.7;
          velocity += normalize(pointVelocity.xyz) * f;
        }
        else {
          // Cohese
          float threshi_delta = 1.0 - alignmentThresh;
          float adjustedPercent = (percent - alignmentThresh) / threshi_delta;
          f  *= (0.5 - cos(adjustedPercent * PI_2) * -0.5 + 0.5) * delt * 0.7;
          velocity += normalize(dir) * f;
        }
      }
    
  }

  if(dot(velocity, velocity) > i_speed * i_speed) {
    velocity = normalize(velocity) * i_speed;
  }

  return vec4(velocity, 1.0);
}

void main() {
	uint i = gl_GlobalInvocationID.x;

	if( i >= numParticles)
	{
		return;
	}

	vec4 velocity = calculateVelocity(pos[i].xyz, vel[i].xyz, int(i));
	vel[i] = velocity;
	vec3 position = calculatePosition(pos[i].xyz, vel[i].xyz);
	pos[i] = vec4(position, 1);
	vel[i] = velocity;
}
